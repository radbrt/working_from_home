*** 

* almost identical as compare

* only thing whether we include those occupations in missing_isco.dta

* here we excluded those, while in compare, we included them. 



local dir "C:\Users\jia\Dropbox\COVID-19\working_home\work"

cd `dir'

clear 

**************************************************************
* some isco-08 unit group occupations are not assigned on MTurk
* assign it manually
**************************************************************


input isco value sd_value wfh_dummy case
1120 1 0 1 0
1439 1 0 1 0
2223 0 0 0 0
2224 0 0 0 0
3139 0 0 0 0
3439 0 0 0 0
5249 0 0 0 0
7319 0 0 0 0
end

save missing_isco, replace


**********input first round answers from MTurk***********************

import delimited ".\raw_data\all_answer.csv", clear 

sort isco 

tabulate answernum, generate(g)

* aggregate over the different answers, and look at the patterns of answers
* note that not all occupations have only 5 labels, few have 10 or 15
* however, it does not impact our analysis much. For those we just scale the answers accordingly. 

collapse (mean) value p1=g1 p2=g2 p3=g3 (sd) sd_value=value (mean) wfh_dummy (count) nobs=value, by(isco) 

***********************************
** Figure 1
***********************************

gen type=p1*1000+p2*100+p3*10

replace type=p2*1000+p1*100+p3*10 if p1<p2

tabulate type

generate type2=floor(type/100)

label define l1 4 "Two persons agree" 6 "Three persons agree" 8 "Four persons agree" 10 "unanimous agreement"

label values type2 l1

graph pie, over(type2) plabel(_all percent, format(%3.0f)) 

graph export "agreement.png", replace

keep isco type2

save round1_type, replace


***** input all answers (round 1,2 and 3)*******************************
***** data generated by Round_3.ipynb **********************************

clear

import delimited "C:\Users\jia\Dropbox\COVID-19\working_home\work\allthree.csv", encoding(Big5) rowrange(2) colrange(2) 

rename v1 isco
rename v2 value
rename v3 sd_value
rename v4 case

* adding in the unlabeld occupations.
append using missing_isco

* adding in the US classification. 
merge 1:1 isco using isco_us_onet 
replace teleworkable_onet=value if missing(teleworkable_onet)


*** keep only those matched
* keep if _merge==3
gen us_yes=(_merge==3)

drop _merge
save isco_all_revision, replace


************************************************
* read in Norwegian employment data 2019 *******
*************************************************

* total shares 

import delimited ".\raw_data\syss.csv", encoding(UTF-8) clear 

merge 1:1 isco using isco_all_revision

keep if _merge==3

drop _merge

merge 1:1 isco using round1_type

preserve

egen total_job=total(lønnstakere2019)

gen weight=lønnstakere2019/total_job

collapse value  teleworkable_onet total_job [aweight=weight]

di "aggregated share: "

list

restore


preserve

egen total_job=total(lønnstakere2019)

keep if type2<8

egen total_job_marginal=total(lønnstakere2019)

keep if _n==1

* share of marginal jobs 

list

di total_job_marginal/total_job

restore

***************************
* ISCO-08 Major groups: Table 1
*****************************

* the lower and upper bound for the marginal occupations (at most three in agreement)
gen value0=value
replace value0=0 if type2<8

gen value1=value
replace value1=1 if type2<8

gen isco_1=floor(isco/1000)

label define isco1 1 "Managers" 2 "Professionals" 3 "Technicians and professionals" 4 "Clerical support workers" 5 "Service and sales" 6 "Skilled primary workers" 7 "Craft workers" 8 "Machine Operator" 9 "Elementary"

label values isco_1 isco1

corr value teleworkable_onet

drop _merge

preserve

bys isco_1: egen total_job=total(lønnstakere2019)

gen weight=lønnstakere2019/total_job

collapse value value0 value1 teleworkable_onet total_job [aweight=weight], by(isco_1)

label variable value "Our measure"
label variable teleworkable_onet "US measure"

list


corr value teleworkable_onet

***************************
* ISCO-08 Major groups: figure 6
*****************************

gen tel=teleworkable_onet-0.05
gen val=value-0.01

scatter value teleworkable_onet [w= total_job ], msymbol(circle_hollow) mlw(vthin) legend(off) xlab(0(0.2)0.9) || line teleworkable_onet teleworkable_onet, lcolor(gs8) lstyle(dash)  legend(off) || scatter val tel, msymbol(none) mlabel(isco_1) mlabsize(vsmall) legend(off)
graph export "US_isco1.png", replace

drop total_job

restore

keep if us_yes==1

save isco_all_employ_revision, replace





**************************************
* difference on wage 
**************************************

import delimited ".\raw_data\yrke_lonn_11418.csv", encoding(UTF-8) varnames(1) rowrange(2:813) clear

gen type=0 
replace type=1 if statistikkmål=="Median"

destring månedslønnkr2019, gen(monthly_wage) force

gen isco=substr(yrke,1,4)
destring isco, replace

keep type monthly_wage isco

reshape wide monthly_wage, i(isco) j(type)

rename monthly_wage0 mean_wage
rename monthly_wage1 median_wage

merge 1:1 isco using isco_all_employ_revision

keep if _merge==3
drop _merge

gen group=2
replace group=1 if value<0.2
replace group=3 if value>0.8001

*******************
* Table 2
***********************

preserve

bys group: egen total_job=total(lønnstakere2019)

gen weight=lønnstakere2019/total_job
collapse mean_wage median_wage total_job (count) number=isco [aweight=weight] , by(group)
list

restore

